buildscript {
   repositories {
      mavenLocal()
      mavenCentral()
   }
}

plugins {
   id 'com.github.spotbugs' version '1.6.11'
   id 'idea'
}

ext {
   assertJVersion = '3.12.0'
   checkstyleVersion = '8.17'
   jacocoVersion = '0.8.3'
   junitVersion = '5.4.1'
   pmdVersion = '6.12.0'
}

allprojects {
   repositories {
      mavenLocal()
      mavenCentral()
   }
}

subprojects {

   group 'romanizer'

   apply plugin: 'java-library'
   apply plugin: 'checkstyle'
   apply plugin: 'com.github.spotbugs'
   apply plugin: 'jacoco'
   apply plugin: 'pmd'

   sourceCompatibility = JavaVersion.VERSION_11
   targetCompatibility = JavaVersion.VERSION_11

   checkstyle {
      toolVersion = checkstyleVersion
   }

   jacoco {
      toolVersion = jacocoVersion
   }

   pmd {
      toolVersion = pmdVersion
      ruleSetFiles = files("$rootDir/config/pmd/pmd.xml")
      ruleSets = []
   }

   spotbugs {
      effort = 'max'
      excludeFilter = file("$rootDir/config/spotbugs/excludeFilter.xml")
   }

   dependencies {
       testImplementation group: 'org.assertj', name: 'assertj-core', version: assertJVersion
       testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junitVersion
   }

   test {
      useJUnitPlatform()
   }
   test.finalizedBy jacocoTestCoverageVerification
}
